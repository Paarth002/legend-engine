###Relational
Database test::store::testDB
(
  Schema PERSON_SCHEMA
  (
    Table PERSON_TABLE_1
    (
      ID INTEGER PRIMARY KEY,
      FIRM_DETAILS VARCHAR(40),
      MANAGER_ID INTEGER
    )
    Table PERSON_TABLE_2
    (
      ID INTEGER PRIMARY KEY,
      FIRM_DETAILS VARCHAR(40),
      MANAGER_ID INTEGER
    )
  )
)


###ExternalFormat
Binding test::binding::FirmBinding
{
  contentType: 'application/json';
  modelIncludes: [
    test::model::Firm
  ];
}


###Pure
Class test::model::Firm
{
  firmName: String[1];
}

Class test::model::Person
{
  firm: test::model::Firm[1];
}

function test::query::getFirmDetails(): meta::pure::tds::TabularDataSet[1]
{
  test::model::Person.all()->project(
    [
      x|$x.firm.firmName
    ],
    ['Firm/FirmName']
  )->from(
    test::mapping::testMapping,
    test::runtime::testRuntime
  )
}

function test::query::getFirmDetailsWithFilter(): meta::pure::tds::TabularDataSet[1]
{
  test::model::Person.all()->filter(
    x|$x.firm.firmName->in(
      [
        'firm_A',
        'firm_B',
        'firm_D'
      ]
    )->and()
  )->project(
    [
      x|$x.firm.firmName
    ],
    ['Firm/FirmName']
  )->from(
    test::mapping::testMapping,
    test::runtime::testRuntime
  )
}


###Mapping
Mapping test::mapping::testMapping
(
  *test::model::Person: Operation
  {
    meta::pure::router::operations::union_OperationSetImplementation_1__SetImplementation_MANY_(Type_A,Type_B)
  }
  test::model::Person[Type_A]: Relational
  {
    ~primaryKey
    (
      [test::store::testDB]PERSON_SCHEMA.PERSON_TABLE_1.ID
    )
    ~mainTable [test::store::testDB]PERSON_SCHEMA.PERSON_TABLE_1
    firm: Binding test::binding::FirmBinding : extractFromSemiStructured([test::store::testDB]PERSON_SCHEMA.PERSON_TABLE_1.FIRM_DETAILS, 'firm', 'VARCHAR')
  }
  test::model::Person[Type_B]: Relational
  {
    ~primaryKey
    (
      [test::store::testDB]PERSON_SCHEMA.PERSON_TABLE_2.ID
    )
    ~mainTable [test::store::testDB]PERSON_SCHEMA.PERSON_TABLE_2
    firm: Binding test::binding::FirmBinding : [test::store::testDB]PERSON_SCHEMA.PERSON_TABLE_2.FIRM_DETAILS
  }
)


###Connection
RelationalDatabaseConnection test::connection::testDBConnection
{
  store: test::store::testDB;
  type: H2;
  specification: LocalH2
  {
    testDataSetupSqls: [
      'Create Schema if not exists PERSON_SCHEMA;',
      'Drop table if exists PERSON_SCHEMA.PERSON_TABLE_1;',
      'Create Table PERSON_SCHEMA.PERSON_TABLE_1(ID INTEGER, FIRM_DETAILS VARCHAR(500), NAME VARCHAR(40));',
      'Insert into PERSON_SCHEMA.PERSON_TABLE_1 (ID, FIRM_DETAILS, NAME) values (1, \'{"firm": {"firmName": "firm_A"}}\', \'ABC\');',
      'Insert into PERSON_SCHEMA.PERSON_TABLE_1 (ID, FIRM_DETAILS, NAME) values (3, \'{"firm": {"firmName": "firm_B"}}\', \'PQR\');',
      'Insert into PERSON_SCHEMA.PERSON_TABLE_1 (ID, FIRM_DETAILS, NAME) values (5, \'{"firm": {"firmName": "firm_C"}}\', \'XYZ\');',
      'Drop table if exists PERSON_SCHEMA.PERSON_TABLE_2;',
      'Create Table PERSON_SCHEMA.PERSON_TABLE_2(ID INTEGER, FIRM_DETAILS VARCHAR(500), NAME VARCHAR(40));',
      'Insert into PERSON_SCHEMA.PERSON_TABLE_2 (ID, FIRM_DETAILS, NAME) values (2, \'{"firmName": "firm_D"}\', \'MNO\');',
      'Insert into PERSON_SCHEMA.PERSON_TABLE_2 (ID, FIRM_DETAILS, NAME) values (4, \'{"firmName": "firm_E"}\', \'DEF\');',
      'Insert into PERSON_SCHEMA.PERSON_TABLE_2 (ID, FIRM_DETAILS, NAME) values (6, \'{"firmName": "firm_F"}\', \'STU\');'
      ];
  };
  auth: DefaultH2;
}


###Runtime
Runtime test::runtime::testRuntime
{
  mappings:
  [
    test::mapping::testMapping
  ];
  connections:
  [
    test::store::testDB:
    [
      test_connection: test::connection::testDBConnection
    ]
  ];
}
